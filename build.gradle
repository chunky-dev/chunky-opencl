plugins {
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.lemaik.de/'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    implementation.extendsFrom(provided)
    implementation.extendsFrom(bundled)
}

dependencies {
    provided 'se.llbit:chunky-core:2.5.0-SNAPSHOT'
    provided 'org.apache.commons:commons-math3:3.2'
    provided 'it.unimi.dsi:fastutil:8.4.4'
    bundled 'org.jocl:jocl:2.0.2'

    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

javafx {
    version = '17'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml']
    configuration = 'provided'
}

processResources {
    from "src/main/opencl/kernel/include", { into "kernel" }
    from "src/main/opencl/tonemap/include", { into "tonemap" }
}

jar {
    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

defaultTasks 'jar'
